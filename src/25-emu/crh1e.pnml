// Begin CRH1E

// Graphics

// Purchase Menu
spriteset (spriteset_crh1e_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25-emu", "crh1e-c")
}

// Vehicle
spriteset (spriteset_crh1e_front, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh1e-c")
}
spriteset (spriteset_crh1e_middle_m, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh1e-m")
}
spriteset (spriteset_crh1e_middle_p, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("25-emu", "crh1e-p")
}
spriteset (spriteset_crh1e_cab, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("25-emu", "crh1e-c")
}

switch (FEAT_TRAINS, SELF, switch_crh1e_articulated_part, extra_callback_info1) {
    1..2: return crh1e;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_graphics, position_in_articulated_veh % 3) {
    1: spriteset_crh1e_front;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_head_capacity, position_in_articulated_veh % 3) {
    1: return 61;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_short_graphics, (position_in_consist_from_end == position_in_consist) || (position_in_consist_from_end == position_in_consist + 3)) {
    1: spriteset_crh1e_middle_p;
    spriteset_crh1e_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_long_graphics, (position_in_consist_from_end == position_in_consist + 12) || (position_in_consist_from_end == position_in_consist + 15) || (position_in_consist_from_end + 12 == position_in_consist) || (position_in_consist_from_end + 15 == position_in_consist)) {
    1: spriteset_crh1e_middle_p;
    spriteset_crh1e_middle_m;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_middle_graphics_1, num_vehs_in_consist) {
    0..24: switch_crh1e_short_graphics;
    switch_crh1e_long_graphics;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_middle_graphics, position_in_articulated_veh % 3) {
    1: switch_crh1e_middle_graphics_1;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_graphics, position_in_consist_from_end) {
    1: spriteset_crh1e_cab;
    switch_crh1e_middle_graphics;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_length, position_in_consist_from_end) {
    switch_length_2_8_2;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_middle_capacity_1, vehicle_type_id) {
    1024: return 93;
    1035: return 58;
    1037: return 40;
    1039: return 16;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_middle_capacity, position_in_articulated_veh % 3) {
    1: switch_crh1e_middle_capacity_1;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_cab_capacity_1, vehicle_type_id) {
    1024: return 61;
    1035: return 28;
    1037: return 28;
    1039: return 12;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_cab_capacity, position_in_articulated_veh % 3) {
    1: switch_crh1e_cab_capacity_1;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_capacity, position_in_consist_from_end) {
    0..2: switch_crh1e_cab_capacity;
    switch_crh1e_middle_capacity;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_cost_power, vehicle_type_id) {
    1024: return 63;
    1035: return 82;
    1037: return 71;
    1039: return 73;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_running_power, vehicle_type_id) {
    1024: return 48;
    1035: return 52;
    1037: return 56;
    1039: return 58;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_cost_unpower, vehicle_type_id) {
    1024: return 25;
    1035: return 44;
    1037: return 33;
    1039: return 35;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_running_unpower, vehicle_type_id) {
    1024: return 10;
    1035: return 14;
    1037: return 19;
    1039: return 20;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_cost, vehicle_is_potentially_powered) {
    1: switch_crh1e_wagon_cost_power;
    switch_crh1e_wagon_cost_unpower;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_running, vehicle_is_potentially_powered) {
    1: switch_crh1e_wagon_running_power;
    switch_crh1e_wagon_running_unpower;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_power, position_in_consist % 48) {
    0..2: return 1441;
    3..5: return 0;
    6..11: return 1441;
    12..14: return 0;
    15..17: return 1441;
    18..20: return 0;
    21..26: return 1441;
    27..29: return 0;
    30..32: return 1441;
    33..35: return 0;
    36..41: return 1441;
    42..44: return 0;
    45..47: return 1441;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_crh1e_wagon_traction, vehicle_is_potentially_powered) {
    1: return int(0.12*255);
    return 0;
}


item (FEAT_TRAINS, crh1e) {
    property {
        // Menu
        name:                           string(STR_NAME_CRH1E);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_MU);
        engine_class:                   ENGINE_CLASS_ELECTRIC;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2009,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SABA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               240 << param_cargo_decay;

        // Stats
        speed:                          250 km/h;
        power:                          1060 kW;
        weight:                         56 ton;
        tractive_effort_coefficient:    0.12;
        cost_factor:                    61;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            26;
        reliability_decay:              8;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_CRH_SLEEPER_CAN_ATTACH_WAGON), string(STR_CRH1E_NICKNAME), string(STR_RELIABILITY_8), string(STR_CRH1E_CONSIST), string(STR_CRH1E_HEAD_SEAT));
        can_attach_wagon:               crhsleeperonly;
        start_stop:                     mu4car;
        // Graphics
        default:                        switch_crh1e_graphics;
        purchase:                       spriteset_crh1e_purchase_original;
        articulated_part:               switch_crh1e_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_crh1e_head_capacity;
        cargo_age_period:               12 * 5 * cafedecayfactor() << param_cargo_decay;
        cost_factor:                    62;
        running_cost_factor:            47 * runningcostfactor() * cafecostfactor() + 20;
        purchase_running_cost_factor:   208;
        purchase_cargo_capacity:        61/3;
    }

    // Multiple Unit Wagons
    livery_override(zemuw) {
        default:                        switch_crh1e_wagon_graphics;
        cargo_capacity:                 switch_crh1e_wagon_capacity;
        power:                          switch_crh1e_wagon_power;
        tractive_effort_coefficient:    switch_crh1e_wagon_traction;
        length:                         switch_crh1e_wagon_length;
        weight:                         56;
        can_attach_wagon:               muhead;
        cargo_age_period:               (12 * 5 * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh1e_wagon_cost;
        running_cost_factor:            switch_crh1e_wagon_running() * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(wymuw) {
        default:                        switch_crh1e_wagon_graphics;
        cargo_capacity:                 switch_crh1e_wagon_capacity;
        power:                          switch_crh1e_wagon_power;
        tractive_effort_coefficient:    switch_crh1e_wagon_traction;
        length:                         switch_crh1e_wagon_length;
        weight:                         56;
        can_attach_wagon:               muhead;
        cargo_age_period:               (32 * 5 * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh1e_wagon_cost;
        running_cost_factor:            switch_crh1e_wagon_running() * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(wgmuw) {
        default:                        switch_crh1e_wagon_graphics;
        cargo_capacity:                 switch_crh1e_wagon_capacity;
        power:                          switch_crh1e_wagon_power;
        tractive_effort_coefficient:    switch_crh1e_wagon_traction;
        length:                         switch_crh1e_wagon_length;
        weight:                         56;
        can_attach_wagon:               muhead;
        cargo_age_period:               (40 * 5 * cafedecayfactor()) << param_cargo_decay;
        bitmask_vehicle_info:           switch_crh_wagon_bitmask;
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh1e_wagon_cost;
        running_cost_factor:            switch_crh1e_wagon_running() * runningcostfactor() * cafecostfactor() + accostfactor();
    }
    livery_override(zecmuw) {
        default:                        switch_crh1e_wagon_graphics;
        cargo_capacity:                 switch_crh1e_wagon_capacity;
        power:                          switch_crh1e_wagon_power;
        tractive_effort_coefficient:    switch_crh1e_wagon_traction;
        length:                         switch_crh1e_wagon_length;
        weight:                         56;
        can_attach_wagon:               muhead;
        cargo_age_period:               (12 * 5 * 5) << param_cargo_decay;
        bitmask_vehicle_info:           bitmask(1);
        articulated_part:               switch_crh_wagon_articulated_part;
        cost_factor:                    switch_crh1e_wagon_cost;
        running_cost_factor:            switch_crh1e_wagon_running() * runningcostfactor() + 50 + accostfactor(); // IT IS A RESTAURANT CAR!
    }
}

// End CRH1E
