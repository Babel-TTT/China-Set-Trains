//Begin GHA70A
switch (FEAT_TRAINS, SELF, switch_gha70a_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_TANKER);
    return CB_RESULT_NO_TEXT;
}

// Graphics

// Purchase Menu
spriteset (spriteset_gha70a_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "gha70a")
}

// Vehicle
spriteset (spriteset_gha70a, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "gha70a")
}
spriteset (spriteset_gha70a_weathered, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "gha70a-weathered")
}

random_switch (FEAT_TRAINS, SELF, random_gha70a_colour) {
    1: spriteset_gha70a;
    1: spriteset_gha70a_weathered;
}

switch (FEAT_TRAINS, SELF, switch_gha70a_cargo_subtype, cargo_subtype) {
    0: return string(STR_RANDOM_COLOR);
    1: return string(STR_FACTORY);
    2: return string(STR_DETERIORATE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_gha70a_graphics_subtype, cargo_subtype) {
    0: random_gha70a_colour;
    1: spriteset_gha70a;
    2: spriteset_gha70a_weathered;
}

switch (FEAT_TRAINS, SELF, switch_gha70a_articulated_part, extra_callback_info1) {
    1..2: return gha70a;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_gha70a_graphics, position_in_articulated_veh % 3) {
    1: switch_gha70a_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_gha70a_capacity, position_in_articulated_veh % 3) {
    1: return 60;
    return 0;
}


item (FEAT_TRAINS, gha70a) {
    property {
        // Menu
        name:                           string(STR_NAME_GHA70A);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					3088;


        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2009,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_LIQUID, CC_HAZARDOUS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             DEFAULT_CARGO_FIRST_REFITTABLE;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 60;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         31.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_FULL_NAME_GHA70A), string(STR_FRESHNESS_MEDIUM, 185));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_gha70a_cargo_subtype;

        // Graphics
        default:                        switch_gha70a_graphics;
        purchase:                       spriteset_gha70a_purchase;
        articulated_part:               switch_gha70a_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_gha70a_capacity;
        cargo_age_period:               185 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    6;
        running_cost_factor:            4 * runningcostfactor();
        purchase_running_cost_factor:   16;
        purchase_cargo_capacity:        60/3;
        name:                           switch_gha70a_name;

    }
}

// End GHA70A

