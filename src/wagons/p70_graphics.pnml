// Begin P70

// Graphics

// Purchase Menu
spriteset (spriteset_p70_purchase_original) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_p70_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "p70")
}

// Vehicle
spriteset (spriteset_p70_black) {
    template_empty ()
}
alternative_sprites(spriteset_p70_black, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p70")
}

random_switch (FEAT_TRAINS, SELF, random_p70_colour) {
    1: spriteset_p70_black; // More colours waiting to be added
}

switch (FEAT_TRAINS, SELF, switch_p70_articulated_part, extra_callback_info1) {
    1..2: return p70;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_p70_graphics, position_in_articulated_veh % 3) {
    1: random_p70_colour;
    empty_sprites;   
}

switch (FEAT_TRAINS, SELF, switch_p70_capacity_cargo, cargo_type_in_veh) {
    MAIL: return 140;
    GOOD: return 105;
    return 70;
}

switch (FEAT_TRAINS, SELF, switch_p70_capacity, position_in_articulated_veh % 3) {
    1: switch_p70_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_p70_loading_speed, cargo_type_in_veh) {
    MAIL: return 10;
    GOOD: return 8;
    return 6;
}

switch (FEAT_TRAINS, SELF, switch_p70_decay, cargo_type_in_veh) {
    LVST: return 560;
    return 185;
}

switch (FEAT_TRAINS, SELF, switch_p70_additional_text, param_cargo_decay) {
    return string(STR_DESC_2, string(STR_P70_LIVERY_AVAILABILITY), string(STR_DECAY_BOXCAR_NOPASS));
}