// Begin P61

// Graphics

// Purchase Menu
spriteset (spriteset_p61_purchase_original) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_p61_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("wagons", "p61")
}

// Vehicle
spriteset (spriteset_p61_black) {
    template_empty ()
}
alternative_sprites(spriteset_p61_black, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("wagons", "p61")
}

random_switch (FEAT_TRAINS, SELF, random_p61_colour) {
    1: spriteset_p61_black; // More colours waiting to be added
}

switch (FEAT_TRAINS, SELF, switch_p61_articulated_part, extra_callback_info1) {
    1..2: return p61;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_p61_graphics, position_in_articulated_veh % 3) {
    1: random_p61_colour;
    empty_sprites;   
}

switch (FEAT_TRAINS, SELF, switch_p61_capacity_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 192;
    return 60;
}

switch (FEAT_TRAINS, SELF, switch_p61_capacity_cargo, cargo_classes == bitmask(CC_MAIL)) {
    1: return 120;
    switch_p61_capacity_pengdaike;
}

switch (FEAT_TRAINS, SELF, switch_p61_capacity, position_in_articulated_veh % 3) {
    1: switch_p61_capacity_cargo;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_p61_loading_speed_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 12;
    return 6;
}

switch (FEAT_TRAINS, SELF, switch_p61_loading_speed, cargo_classes == bitmask(CC_MAIL)) {
    1: return 8;
    switch_p61_loading_speed_pengdaike;
}

switch (FEAT_TRAINS, SELF, switch_p61_decay_pengdaike, (cargo_classes == bitmask(CC_PASSENGERS)) || (cargo_type_in_veh == YETI) || (cargo_type_in_veh == YETY)) {
    1: return 80;
    return 185;
}

switch (FEAT_TRAINS, SELF, switch_p61_decay, cargo_type_in_veh == LVST) {
    1: return 560;
    switch_p61_decay_pengdaike;
}

switch (FEAT_TRAINS, SELF, switch_p61_additional_text, param_cargo_decay) {
    return string(STR_DESC_3, string(STR_P61_LIVERY_AVAILABILITY), string(STR_DECAY_BOXCAR), string(STR_DESC_PENGDAIKE));
}