// Begin HA1
/* switch (FEAT_TRAINS, SELF, switch_smr_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_SMR_COACHES_SERIES);
    return CB_RESULT_NO_TEXT;
} */
// Graphics

// Purchase Menu
spriteset (spriteset_ha1_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/smr", "ha1")
}

// Vehicle
spriteset (spriteset_ha1_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/smr", "ha1")
}

switch (FEAT_TRAINS, SELF, switch_ha1_articulated_part, extra_callback_info1) {
    1..2: return ha1;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}



switch (FEAT_TRAINS, SELF, switch_ha1_graphics, position_in_articulated_veh % 3) {
    1: spriteset_ha1_original;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ha1_capacity, position_in_articulated_veh % 3) {
    1: return 122;
    return 0;
}


item (FEAT_TRAINS, ha1) {
    property {
        // Menu
        name:                           string(STR_NAME_HA1);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1916,01,01);
        model_life:                     35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          75 km/h;
        power:                          0 hp;
        weight:                         35.9 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_SMR_VEHICLE), string(STR_COMFORT_LOW, 144), string(STR_NO_AIR_CONDITIONER));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_ha1_graphics;
        purchase:                       spriteset_ha1_purchase_original;
        articulated_part:               switch_ha1_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 switch_ha1_capacity;
        cargo_age_period:               (9 * 4 * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    7;
        running_cost_factor:            5 * runningcostfactor() * cafecostfactor(); // NO AC FUNCTION
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        122/3;
/*         name:                           switch_smr_name; */
    }
}

// End HA1