// Begin YZ22

// Graphics

// Purchase Menu
spriteset (spriteset_22_purchase_original) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_22_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "22")
}

// Vehicle
spriteset (spriteset_22_original) {
    template_empty ()
}
spriteset (spriteset_22_wru) {
    template_empty ()
}
spriteset (spriteset_yz22_jy) {
    template_empty ()
}
alternative_sprites(spriteset_22_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "22")
}
alternative_sprites(spriteset_22_wru, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "22-wru")
}
alternative_sprites(spriteset_yz22_jy, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "yz22-jy")
}

switch (FEAT_TRAINS, SELF, switch_yz22_articulated_part, extra_callback_info1) {
    1..2: return yz22;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_yz22_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_WRU);
    2: return string(STR_JY);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_yz22_graphics_subtype, cargo_subtype) {
    0: spriteset_22_original;
    1: spriteset_22_wru;
    2: spriteset_yz22_jy;
}

switch (FEAT_TRAINS, SELF, switch_yz22_graphics, position_in_articulated_veh % 3) {
    1: switch_yz22_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_yz22_capacity, position_in_articulated_veh % 3) {
    1: return 118;
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_yz22_additional_text, param_cargo_decay) {
    return string(STR_DESC_4, string(STR_FULL_NAME_YZ22), string(STR_22_LIVERY_AVAILABILITY), string(STR_COMFORT_144), string(STR_NO_AIR_CONDITIONER));
}