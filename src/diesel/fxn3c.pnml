// Begin FXN3C
// Graphics

// Purchase Menu
spriteset (spriteset_fxn3c_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "fxn3/fxn3c")
}

// Vehicle
spriteset (spriteset_fxn3c_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "fxn3/fxn3c")
}

spriteset (spriteset_fxn3c_prototype, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "fxn3/fxn3c-prototype")
}

/* spriteset (spriteset_fxn3c0001, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "extra/fxn3c-0001")
} */

switch (FEAT_TRAINS, SELF, switch_fxn3c_articulated_part, extra_callback_info1) {
    1..2: return fxn3c;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_fxn3c_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_PROTOTYPE);
/*     2: return string(STR_CHN2LAOS); */
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_fxn3c_graphics_subtype, cargo_subtype) {
    0: spriteset_fxn3c_original;
    1: spriteset_fxn3c_prototype;
/*     2: spriteset_fxn3c0001; */
}

switch (FEAT_TRAINS, SELF, switch_fxn3c_graphics, position_in_articulated_veh % 3) {
    1: switch_fxn3c_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, fxn3c) {
    property {
        // Menu
        name:                           string(STR_NAME_FXN3C);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2019,10,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   35;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          3250 kW;
        weight:                         150 ton;
        tractive_effort_coefficient:    0.3945;
        cost_factor:                    1;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            1;
        reliability_decay:              8;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_FXN3C), string(STR_LV_AVAILABLE_S ,string(STR_LIVERY_AVAILABLE), string(STR_ORIGINAL), string(STR_PROTOTYPE)), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELDEC_VERY_LOW, 5));
        cargo_subtype_text:             switch_fxn3c_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_fxn3c_graphics;
        purchase:                       spriteset_fxn3c_purchase;
        articulated_part:               switch_fxn3c_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        cargo_capacity:                 0;
        cost_factor:                    98;
        running_cost_factor:            83 * runningcostfactor();
        purchase_running_cost_factor:   332;
        purchase_cargo_capacity:        0;
/*         name:                           switch_fxn3_name; */
    }
}

// End FXN3C
