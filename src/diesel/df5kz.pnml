// Begin DF5KZ

// Graphics

// Purchase Menu
spriteset (spriteset_df5kz_purchase) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_df5kz_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df5kz")
}

// Vehicle
spriteset (spriteset_df5kz_original) {
    template_empty ()
}
spriteset (spriteset_df5kz_alternative) {
    template_empty ()
}
spriteset (spriteset_df5kz_mzd) {
    template_empty ()
}
alternative_sprites(spriteset_df5kz_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5kz")
}
alternative_sprites(spriteset_df5kz_alternative, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5kz-alternative")
}
alternative_sprites(spriteset_df5kz_mzd, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df5kz-mzd")
}

switch (FEAT_TRAINS, SELF, switch_df5kz_articulated_part, extra_callback_info1) {
    1..2: return df5kz;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df5kz_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORIGINAL);
    1: return string(STR_ALTERNATIVE);
    2: return string(STR_MZD);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df5kz_graphics_subtype, cargo_subtype) {
    0: spriteset_df5kz_original;
    1: spriteset_df5kz_alternative;
    2: spriteset_df5kz_mzd;
}

switch (FEAT_TRAINS, SELF, switch_df5kz_graphics, position_in_articulated_veh % 3) {
    1: switch_df5kz_graphics_subtype;
    empty_sprites;   
}

switch (FEAT_TRAINS, SELF, switch_df5kz_capacity, position_in_articulated_veh % 3) {
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_df5kz_additional_text, param_cargo_decay) {
    return string(STR_DESC_3, string(STR_FULL_NAME_DF5KZ), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELIABILITY_20));
}


item (FEAT_TRAINS, df5kz) {
    property {
        // Menu
        name:                           string(STR_NAME_DF5KZ);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;
        
        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1976,01,01);
        model_life:                     60;
        retire_early:                   30;
        vehicle_life:                   25;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          930 kW;
        weight:                         120 ton;
        tractive_effort_coefficient:    0.323;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              20;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                switch_df5kz_additional_text;
        cargo_subtype_text:             switch_df5kz_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df5kz_graphics;
        purchase:                       spriteset_df5kz_purchase;
        articulated_part:               switch_df5kz_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    33;
        running_cost_factor:            28 * runningcostfactor();
        purchase_running_cost_factor:   112;
        purchase_cargo_capacity:        0;
    }
}

// End DF5KZ

