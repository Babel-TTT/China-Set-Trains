//Begin GC220

// Graphics

// Purchase Menu
spriteset (spriteset_gc220_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "gc220")
}

// Vehicle
spriteset (spriteset_gc220_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "gc220")
}

spriteset (spriteset_gc220_blue, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "gc220-blue")
}

spriteset (spriteset_gc220_orange, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "gc220-orange")
}

switch (FEAT_TRAINS, SELF, switch_gc220_articulated_part, extra_callback_info1) {
    1..2: return gc220;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_gc220_cargo_subtype, cargo_subtype) {
    0: return string(STR_RED);
    1: return string(STR_BLUE);
    2: return string(STR_ORANGE);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_gc220_graphics_subtype, cargo_subtype) {
    0: spriteset_gc220_original;
    1: spriteset_gc220_blue;
    2: spriteset_gc220_orange;
}

switch (FEAT_TRAINS, SELF, switch_gc220_graphics, position_in_articulated_veh % 3) {
    1: switch_gc220_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_gc220_capacity_cargo, cargo_type_in_veh) {
    PASS: return 20;
    MAIL: return 40;
    GOOD: return 0;
}

switch (FEAT_TRAINS, SELF, switch_gc220_capacity, position_in_articulated_veh % 3) {
    1: switch_gc220_capacity_cargo;
    return 0;
}

item (FEAT_TRAINS, gc220) {
    property {
        // Menu
        name:                           string(STR_NAME_GC220);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(2005,01,01);
        model_life:                     50;
        retire_early:                   35;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, MAIL, GOOD];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 20;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          220 kW;
        weight:                         24 ton;
        tractive_effort_coefficient:    0.200;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            50;
        reliability_decay:              24;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_GC220), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_GC220_LIVERY_AVAILABILITY), string(STR_RELDEC_HIGH, 24));
        cargo_subtype_text:             switch_gc220_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_gc220_graphics;
        purchase:                       spriteset_gc220_purchase;
        articulated_part:               switch_gc220_articulated_part;

        // Stats
        length:                         switch_length_2_2_2;
        cargo_capacity:                 switch_gc220_capacity;
        cost_factor:                    16;
        running_cost_factor:            17 * runningcostfactor();
        purchase_running_cost_factor:   68;
        purchase_cargo_capacity:        switch_gc220_capacity_cargo() / 3;

    }
}

// End GC220

