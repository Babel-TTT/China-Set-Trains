// Begin DF11G

// Graphics

// Purchase Menu
spriteset (spriteset_df11g_purchase) {
    template_purchase_empty ()
}
alternative_sprites(spriteset_df11g_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase1 ("diesel", "df11g")
}


// Vehicle
spriteset (spriteset_df11g_leaping) {
    template_empty ()
}
spriteset (spriteset_df11g_leaping_reversed) {
    template_empty ()
}
alternative_sprites(spriteset_df11g_leaping, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df11g")
}
alternative_sprites(spriteset_df11g_leaping_reversed, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_reversed ("diesel", "df11gr")
}

switch (FEAT_TRAINS, SELF, switch_df11g_articulated_part, extra_callback_info1) {
    1..2: return df11g;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// "Splitting Pig"

switch (FEAT_TRAINS, SELF, switch_df11g_graphics_joint, position_in_vehid_chain % 6) {
    1: spriteset_df11g_leaping;
    4: spriteset_df11g_leaping_reversed;
    empty_sprites;   
}

switch (FEAT_TRAINS, SELF, switch_df11g_graphics_front, position_in_articulated_veh % 3) {
    1: spriteset_df11g_leaping;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df11g_graphics_split, position_in_consist_from_end) {
    1: spriteset_df11g_leaping_reversed;
    switch_df11g_graphics_front;
}

switch (FEAT_TRAINS, SELF, switch_df11g_graphics, num_vehs_in_vehid_chain % 6) {
    0: switch_df11g_graphics_joint;
    3: switch_df11g_graphics_split;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df11g_speed, num_vehs_in_vehid_chain % 6) {
    0: return 170;
    return 120;
}

switch (FEAT_TRAINS, SELF, switch_df11g_power_split, position_in_consist_from_end) {
    0..2: return 0;
    return 4133;
}

switch (FEAT_TRAINS, SELF, switch_df11g_power, num_vehs_in_vehid_chain % 6) {
    3: switch_df11g_power_split;
    return 4133;
}

switch (FEAT_TRAINS, SELF, switch_df11g_traction_split, vehicle_is_potentially_powered) {
    0: return 0;
    return int(0.143*255);
}

switch (FEAT_TRAINS, SELF, switch_df11g_traction, num_vehs_in_vehid_chain % 6) {
    3: switch_df11g_traction_split;
    return int(0.143*255);
}

switch (FEAT_TRAINS, SELF, switch_df11g_capacity, position_in_articulated_veh % 3) {
    return 0;
}

switch (FEAT_TRAINS, SELF, switch_df11g_additional_text, param_cargo_decay) {
    return string(STR_DESC_5, string(STR_ELECTRICITY_SUPPLY_YES), string(STR_DF11G_NICKNAME), string(STR_RELIABILITY_6), string(STR_DF11G_CONSIST), string(STR_LEAPING_LIU_NEVER_DIES));
}